action("brave-extension") {
  # Need this to fire re-pack if changed, nevertheless extension is repacked on each 2nd build
  inputs = [
    "app/components/braveShields/braveShields.tsx",
    "app/components/braveShields/braveShieldsControls.tsx",
    "app/components/braveShields/braveShieldsFooter.tsx",
    "app/components/braveShields/braveShieldsHeader.tsx",
    "app/components/braveShields/braveShieldsStats.tsx",
    "app/braveShieldsPanel.tsx",
  ]
  script = "py-scripts/build-brave-extension.py"
  args = [ rebase_path("../.."), rebase_path(root_build_dir) ]
  outputs = [
    "$target_gen_dir/brave_extension/build/background.html",
    "$target_gen_dir/brave_extension/build/js/background.bundle.js",
    "$target_gen_dir/brave_extension/build/js/content.bundle.js",
    "$target_gen_dir/brave_extension/build/js/braveShieldsPanel.bundle.js",
    "$target_gen_dir/brave_extension/build/braveShieldsPanel.html",
    "$target_gen_dir/brave_extension/build/bravelizer.css",
    "$target_gen_dir/brave_extension/build/img/icon-16.png",
    "$target_gen_dir/brave_extension/build/img/icon-32.png",
    "$target_gen_dir/brave_extension/build/img/icon-48.png",
    "$target_gen_dir/brave_extension/build/img/icon-64.png",
    "$target_gen_dir/brave_extension/build/img/icon-128.png",
    "$target_gen_dir/brave_extension/build/img/icon-256.png",
    "$target_gen_dir/brave_extension/build/img/icon.svg",
    "$target_gen_dir/brave_extension/build/img/icon-off.svg",
    "$target_gen_dir/brave_extension/build/_locales/en_US/messages.json",
  ]

  # For macOS deps is bounded in //chrome:chrome_framework
  # For Windows installer copying is done in \\chrome\tools\build\win\create_installer_archive.py
  # Deps below is used for Linux and debugging environment output dir on Windows
  if (!is_mac) {
    deps = [":copy-extension-localization"]
  }
}

if (is_mac) {
  bundle_data("brave_extension_framework_bundle_data") {
    sources = [ "$root_out_dir/resources/brave_extension" ]
    outputs = [ "{{bundle_resources_dir}}/{{source_file_part}}" ]

    public_deps = [ ":copy-extension-localization" ]
  }
}

action("copy-extension-localization") {
  script = "py-scripts/copy-brave-extension-localization.py"

  # changes of something from the list below makes script re-run
  sources = [
    "//brave/vendor/brave-extension/app/_locales/en_US/messages.json",
  ]

  resources_target_dir = "$root_build_dir/resources"

  args = [
    rebase_path("$resources_target_dir"),
  ]

  outputs = [
    "$resources_target_dir/brave_extension",
  ]
}
